plugins {
    id 'com.android.library'
}

evaluationDependsOn(':tests')

version          = version_number
group            = group_info

android {
    namespace = 'org.whispersystems.libsignal'
    compileSdk = 34

    defaultConfig {
        minSdkVersion = 21
        targetSdkVersion = 34
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    testOptions {
        unitTests.all { unitTestTask ->
            unitTestTask.dependsOn project(':tests').tasks.named('testClasses')
            unitTestTask.testClassesDirs.from(project(':tests').sourceSets.test.output.classesDirs)
            unitTestTask.classpath += project(':tests').sourceSets.test.runtimeClasspath
        }
    }
}

repositories {
    google()
    mavenCentral()
    mavenLocal()
}

dependencies {
    api(project(':java'))
    testImplementation project(path: ':tests', configuration: 'testArtifacts')
}
