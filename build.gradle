plugins {
    id 'com.android.application' version '8.13.0'
    id 'com.android.library' version '8.13.0' apply false
}

repositories {
    maven {
        url = uri("https://repo1.maven.org/maven2/")
    }
    google()
    mavenCentral()
    mavenLocal()
    maven { url = uri('https://jitpack.io') }
    maven { url = uri('https://guardianproject.info/maven') }
}

subprojects {
    ext.version_number     = "2.4.0"
    ext.group_info         = "org.whispersystems"
    ext.curve25519_version = "0.5.0"
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.core:core:1.13.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.viewpager:viewpager:1.1.0'
    implementation 'androidx.interpolator:interpolator:1.0.0'
    implementation 'androidx.loader:loader:1.1.0'
    implementation 'androidx.cursoradapter:cursoradapter:1.0.0'

    implementation 'se.emilsjolander:stickylistheaders:2.7.0'
    implementation 'com.jpardogo.materialtabstrip:library:1.0.9'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.github.chrisbanes.photoview:library:1.2.3'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.makeramen:roundedimageview:2.1.0'
    implementation 'com.pnikosis:materialish-progress:1.5'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'pl.tajchert:waitingdots:0.1.0'
    implementation 'com.melnykov:floatingactionbutton:1.3.0'
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
    implementation 'com.google.zxing:core:3.5.3'
    implementation('com.davemorrissey.labs:subsampling-scale-image-view:3.6.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    implementation("com.doomonafireball.betterpickers:library:1.5.3") {
        exclude group: 'com.android.support', module: 'support-v4'
    }

    implementation project(':org.whispersystems.jobmanager')
    implementation project(':org.whispersystems.libpastelog')
    implementation project(':org.whispersystems.libsignal')

    implementation 'com.annimon:stream:1.2.2'

    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.17.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.13.35'

    implementation project(':com.amulyakhare.textdrawable')

    implementation 'com.klinkerapps:android-smsmms:5.2.1'

    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.mockito:mockito-inline:3.12.4'

    androidTestImplementation 'androidx.multidex:multidex:2.0.1'
    androidTestImplementation 'androidx.multidex:multidex-instrumentation:2.0.0'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.6.1'
    androidTestImplementation('org.assertj:assertj-core:3.24.2') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    androidTestImplementation('com.squareup.assertj:assertj-android:1.2.0') {
        exclude group: 'org.hamcrest',        module: 'hamcrest-core'
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}


android {
    namespace = 'org.smssecure.smssecure'
    compileSdk = 34

    defaultConfig {
        applicationId = 'org.smssecure.smssecure'
        versionCode = 212
        versionName = "0.16.13-unstable"

        minSdkVersion = 21
        targetSdkVersion = 34

    testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'

        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters += ['armeabi-v7a', 'arm64-v8a']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        buildConfig = true
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                it.jvmArgs += [
                        '--add-opens=java.base/java.lang=ALL-UNNAMED',
                        '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED',
                        '--add-opens=java.base/java.io=ALL-UNNAMED',
                        '--add-opens=java.base/java.util=ALL-UNNAMED',
                        '--add-opens=java.base/java.text=ALL-UNNAMED',
                        '--add-opens=java.base/java.security=ALL-UNNAMED',
                        '--add-opens=java.base/javax.crypto=ALL-UNNAMED',
                        '--add-opens=java.base/javax.crypto.spec=ALL-UNNAMED',
                        '--add-opens=java.base/java.lang.invoke=ALL-UNNAMED',
                        '--add-opens=java.base/jdk.internal.access=ALL-UNNAMED',
                        '--add-opens=java.base/jdk.internal.misc=ALL-UNNAMED',
                        '--add-opens=java.base/jdk.internal.reflect=ALL-UNNAMED',
                        '--add-opens=java.base/java.util.concurrent=ALL-UNNAMED',
                        '--add-opens=java.base/java.lang.module=ALL-UNNAMED',
                        '-Dnet.bytebuddy.experimental=true'
                ]
            }
        }
    }

    packaging {
        resources {
            excludes += ['LICENSE.txt', 'LICENSE', 'NOTICE', 'asm-license.txt', 'META-INF/LICENSE', 'META-INF/NOTICE']
        }
        jniLibs {
            keepDebugSymbols += ['**/libcurve25519.so']
        }
    }

    buildTypes {
        debug {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
        }
        release {
            minifyEnabled = true
            proguardFiles = buildTypes.debug.proguardFiles
            testProguardFiles = buildTypes.debug.testProguardFiles
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        androidTest {
            java.srcDirs = ['test/androidTest/java']
        }
        test {
            java.srcDirs = ['test/unitTest/java']
        }
    }

}
def props = new Properties()
def propFile = file('signing.properties')
if (propFile.canRead()) {
    propFile.withInputStream { props.load(it) }
}

def credentialFor = { String key ->
    def trimmed = props.getProperty(key)?.trim()
    if (trimmed) {
        return trimmed
    }
    return System.getenv("SILENCE_${key}")?.trim()
}

def storeFileValue     = credentialFor('STORE_FILE')
def storePasswordValue = credentialFor('STORE_PASSWORD')
def keyAliasValue      = credentialFor('KEY_ALIAS')
def keyPasswordValue   = credentialFor('KEY_PASSWORD')

if (storeFileValue && storePasswordValue && keyAliasValue && keyPasswordValue) {
    android {
        signingConfigs {
            release {
                storeFile     = file(storeFileValue)
                storePassword = storePasswordValue
                keyAlias      = keyAliasValue
                keyPassword   = keyPasswordValue
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
} else if (propFile.canRead()) {
    println 'signing.properties found but some entries are missing; checked environment variables as SILENCE_* as well.'
} else {
    println 'signing credentials missing: define SILENCE_* environment variables or signing.properties entries.'
}

allprojects {
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.removeAll { it in ['-Xlint:-deprecation', '-Xlint:-unchecked'] }
        options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked']
        options.deprecation = true
        options.warnings = true
    }
}
